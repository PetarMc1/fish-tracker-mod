plugins {
    id 'fabric-loom' version '1.14-SNAPSHOT'
    id 'maven-publish'
}

base {
    archivesName = project.archives_base_name
}

version = project.mod_version
group = project.maven_group


repositories {
    maven { url "https://maven.shedaniel.me/" }
    maven { url "https://maven.petarmc.com/releases/"}
//    maven {
//        url "https://maven.petarmc.com/private/"
//        credentials {
//            username = project.findProperty('repoUsername')
//            password = project.findProperty('repoPassword')
//        }
//    }
}

dependencies {
    minecraft "com.mojang:minecraft:${project.minecraft_version}"
    mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"
    modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"
    modApi("me.shedaniel.cloth:cloth-config-fabric:${project.cloth_config_ver}") {exclude(group: "net.fabricmc.fabric-api")}
    modImplementation "com.petarmc:lib:${project.lib_ver}"
    implementation 'com.macasaet.fernet:fernet-java:1.5.0'
    modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"
}

processResources {
    inputs.property "version", project.version
    inputs.property "minecraft_version", project.minecraft_version
    inputs.property "loader_version", project.loader_version
    filteringCharset "UTF-8"

    filesMatching("fabric.mod.json") {
        expand "version": project.version,
                "minecraft_version": project.minecraft_version,
                "loader_version": project.loader_version,
                "lib_version": project.lib_ver,
                "cloth_config_version": project.cloth_config_ver
    }
}

def targetJavaVersion = 21
tasks.withType(JavaCompile).configureEach {
    it.options.encoding = "UTF-8"
    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        it.options.release.set(targetJavaVersion)
    }
}

java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
    withSourcesJar()
}

jar {
    from("LICENSE") {
        rename { "${it}_${project.archives_base_name}" }
    }
}

tasks.register('buildAllVersions') {
    group = 'build'

    doLast {
        def gradleScript = System.getProperty("os.name").toLowerCase().contains("win") ? "gradlew.bat" : "./gradlew"
        def builtVersions = []
        def outputDir = file('build/libs')
        if (!outputDir.exists()) outputDir.mkdirs()

        def hasBuildNumber = project.hasProperty('buildNumber')
        def buildNumber = hasBuildNumber ? project.buildNumber : "local"
        def modVersion = project.version
        def modName = project.name

        def makeFileName = { mcVersion ->
            if (hasBuildNumber) {
                return "${modName}-mc${mcVersion}-${buildNumber}.jar"
            } else {
                return "${modName}-mc${mcVersion}-v${modVersion}.jar"
            }
        }

        def builds = []

        def defaultId = 'default'
        def defaultLabel = "Default (Minecraft ${project.minecraft_version})"
        def defaultCmd = "${gradleScript} build -PbuildDir=build/${defaultId}"
        builds.add([id: defaultId, mcVersion: project.minecraft_version, fabricVersion: project.fabric_version, cmd: defaultCmd, label: defaultLabel])

        fileTree(dir: 'props', include: '*.properties').each { propFile ->
            Properties props = new Properties()
            propFile.withInputStream { stream -> props.load(stream) }

            def mcVersion = props.getProperty('minecraft_version')
            def fabricVersion = props.getProperty('fabric_version')
            def id = mcVersion
            def label = "Minecraft ${mcVersion} (Fabric ${fabricVersion})"
            def cmd = "${gradleScript} build -PbuildDir=build/${id} " +
                    "-Pminecraft_version=${mcVersion} " +
                    "-Pyarn_mappings=${props.getProperty('yarn_mappings')} " +
                    "-Ploader_version=${props.getProperty('loader_version')} " +
                    "-Pfabric_version=${fabricVersion}"
            builds.add([id: id, mcVersion: mcVersion, fabricVersion: fabricVersion, cmd: cmd, label: label])
        }

        def processes = []
        builds.each { build ->
            println("Starting build for ${build.label}...")
            def proc = build.cmd.execute()
            processes.add(proc)
        }

        processes.each { it.waitFor() }
        builtVersions.addAll(builds.collect { it.label })

        builds.each { build ->
            def buildLibsDir = file("build/${build.id}/libs")
            fileTree(dir: buildLibsDir, include: '*.jar', exclude: '*-sources.jar').each { jar ->
                def newName = makeFileName(build.mcVersion)
                def destFile = new File(outputDir, newName)
                jar.renameTo(destFile)
            }
        }

        println("Built versions: ${builtVersions}")
    }
}
